top := `git rev-parse --show-toplevel`
output := top + "/bin"
binary := "connect-client"

build version os='linux' arch='amd64':
    #!/usr/bin/env bash
    set -euxo pipefail

    subdir="{{ os }}-{{ arch }}"
    mkdir -p "{{ output }}/$subdir"

    GOOS={{ os }} GOARCH={{ arch }} \
    go build \
        -ldflags "connect-client/project.Version={{ version }}" \
        -o "{{ output }}" \
        ./cmd/connect-client
    cd "{{ output }}"

    if [[ "{{ os }}" == "windows" ]]; then
        target="{{ binary }}.exe"
    else
        target="{{ binary }}"
    fi
    mv {{ output }}/connect-client* "{{ output }}/$subdir/$target"

build-all version:
    just build {{ version }} linux amd64
    just build {{ version }} windows amd64
    just build {{ version }} darwin amd64
    just build {{ version }} darwin arm64

lint:
    ./fmt-check.sh
    go vet -all ./...
    shadow ./...

test *args:
    #!/usr/bin/env bash
    set -euxo pipefail

    test_args="{{ args }}"
    test_args=${test_args:-./...}

    go test -race ${test_args}
