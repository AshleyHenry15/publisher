alias c := clean
alias i := install
alias t := test

export CONTENT := env_var_or_default("CONTENT", "all")

_ci := env_var_or_default("CI", "false")

_debug := env_var_or_default("DEBUG", "false")

_with_debug := if _debug == "true" {
        "set -x pipefail"
    } else {
        ""
    }

default:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    just install
    just test

# Deletes ephemeral project files (i.e., cleans the project).
clean:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    rm -rf node_modules


# Install dependencies
install:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm install

# Executes commands via `npm`. Equivalent to `npm run`. Provides arbitrary command chaining inside of Docker from project root (i.e., `just bats run test`)
run *args:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    npm run {{ args }}


# Executes bats deploy tests
test-contract:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    exe=$(just ../../executable-path)

    if [ ! -f $exe ]; then
        echo "error: Missing executable $exe. Run \`just build\`." 1>&2
        exit 1
    fi
    pip install -r ../setup/requirements.txt
    export CONNECT_SERVER="$(python ../setup/connect_setup.py)"
    export CONNECT_API_KEY="$(python ../setup/gen_apikey.py 'admin')"
    echo "CONTENT: ${CONTENT}"
    EXE=$exe npm run accounts
    if [[ "${CONTENT}" == "all" ]]; then
        content_list=$(python ../deploy_helper.py)
        for i in ${content_list}
        do
            export CONTENT=${i}
            EXE=$exe npm run deploy
        done
    else
        EXE=$exe npm run deploy
    fi
    
    

# Executes bats local tests
test:
    #!/usr/bin/env bash
    set -eou pipefail
    {{ _with_debug }}

    exe=$(just ../../executable-path)

    if [ ! -f $exe ]; then
        echo "error: Missing executable $exe. Run \`just build\`." 1>&2
        exit 1
    fi
    
    EXE=$exe npm run cli