set shell := ["bash", "-uc"]

export TARGET_SERVER := env_var_or_default("TARGET_SERVER", "http://localhost:3939")
# export API_KEY := env_var_or_default("PERFTEST_API_KEY", `echo -n admin | md5sum | cut -f1 -d" "`)

build target:
  #!/usr/bin/env bash
  docker build \
    --build-arg BUILDKIT_INLINE_CACHE=1 \
    --pull \
    --tag rstudio/connect-client-{{target}} \
    ./docker

init-connect:
	docker compose pull connect && \
	docker compose up -d connect

run-client target:
  #!/usr/bin/env bash
  if [[ {{target}} =~ "linux-amd64" ]]; then
    docker run \
    --platform linux/amd64 \
    -e BATS_SUPPORT_LIB="/libs/bats-support/load" \
    -e BATS_ASSERT_LIB="/libs/bats-assert/load" \
    -e BINARY_PATH=bin/{{target}}/connect-client \
    -v "$(pwd)"/../:/publishing-client \
    -w /publishing-client/test \
    --rm rstudio/connect-client-{{target}} just _test-client {{target}}
  elif [[ {{target}} =~ "linux-arm64" ]]; then
    docker run \
    --platform linux/arm64 \
    -e BATS_SUPPORT_LIB="/libs/bats-support/load" \
    -e BATS_ASSERT_LIB="/libs/bats-assert/load" \
    -e BINARY_PATH=../bin/{{target}}/connect-client \
    -v "$(pwd)"/../:/publishing-client \
    -w /publishing-client/test \
    --rm rstudio/connect-client-{{target}} just _test-client {{target}}
  elif [[ {{target}} =~ "windows" ]]; then
    just _test-client windows-amd64
  elif [[ {{target}} =~ "macos" ]]; then
    just _test-client darwin-amd64
  fi

_test-client target:
  bats --tap -T -r ./bats/

run-client-local target:
  #!/usr/bin/env bash
  just bats-install && \
  BATS_SUPPORT_LIB={{invocation_directory()}}/libs/bats-support/load \
  BATS_ASSERT_LIB={{invocation_directory()}}/libs/bats-assert/load \
  BINARY_PATH=../bin/{{target}}/connect-client \
  ./libs/bats-core/bin/bats --tap -T -r ./bats/

bats-install:
  #!/usr/bin/env bash
  if [[ ! -d ./libs ]]; then
    git clone --depth=1 https://github.com/bats-core/bats-core.git ./libs/bats-core && \
    ./libs/bats-core/install.sh ./libs/bats-core/installation && \
    git clone --depth=1 https://github.com/ztombol/bats-support.git ./libs/bats-support && \
    git clone --depth=1 https://github.com/ztombol/bats-assert.git ./libs/bats-assert
  fi
